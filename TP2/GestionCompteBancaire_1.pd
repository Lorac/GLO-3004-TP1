// Équipe #1
// Antoine Giasson (111 044 655), GLO
// Maxime Charron (111 038 084), GLO
// Maxime Roussin-Bélanger (111 018 750), IFT
// Julien Duchesne (111 042 624), GLO

// Types pour ATM

class ETAT ^= enum inactif , carteIns ,nipOk, retrait , chNIP end ;

class BOUTON ^= enum cash , nip end ;

class NCompte ^= NumCompte || void;

//    fin

// numéro de banque maximal

const maxNum : int ^= 29999;

class NumCompte ^= those j: int :- 1 <= j <= maxNum   ;

class NumBanque ^= those j: int :- 1 <= j <= maxNum   ;

class Jour ^= those j: int :- 1 <= j <= 31;
class Mois ^= those m : int :- 1 <= m <= 12; 
class An ^= those a : int :- 1900 <= a <= 20000; 

class Date ^= 
abstract
var 
   j: Jour,
   m : Mois, 
   a : An; 
  invariant 
    ( [m in set of int {1,3,5,7,8,10,12}] : 1 <= j & j <= 31,
	  [ m in set of int {4,6,9,11}] : 1 <= j & j <= 30, 
	  [ m = 2 & (a % 4 = 0 )&(a % 100 ~= 0)] : 
	    1 <= j <= 29, 
      [ m = 2 &  (a % 400 = 0) ]: 1 <= j & j <= 29, 
	   [ ] : 1 <= j <= 28
	);
  interface 
   build{!j:Jour, !m:Mois,!a:An}
    pre 
	 ( [m in set of int {1,3,5,7,8,10,12}] : 1 <= j & j <= 31,
	  [ m in set of int {4,6,9,11}] : 1 <= j & j <= 30, 
	  [ m = 2 & (a % 4 = 0 )&(a % 100 ~= 0)] : 
	    1 <= j <= 29, 
      [ m = 2 &  (a % 400 = 0) ]: 1 <= j & j <= 29, 
	   [ ] : 1 <= j <= 28
	) ;
    
  
     function j, m, a
end;
// Dat est utilisée pour la date de fermeture. 
// null veut dire le compte est ouvert
class Dat ^= Date || void; 
const maxLiquide : int ^= 1000;
const minSolde : int ^= 50;

final class Compte ^= 
abstract
  var 
	solde : int, 
	nip : int, 
	ouverture : Date, 
	fermeture : Dat,
	liquide : int;
	
invariant
	liquide <= maxLiquide,
	(fermeture within Date) | (solde >= minSolde);
   
interface
  build{!solde:int,!nip:int,!ouverture:Date,!fermeture : Dat}
	pre (solde >= minSolde) | (fermeture within Date)
	post liquide! = 0;
	
  schema !retrait(n : int)
	pre 
		n > 0,
		solde - n >= minSolde
	post solde! = solde - n;
	
  schema !depot(n : int)
	pre n > 0
	post solde! = solde + n;
	
  schema !depotLiquide(n : int)
    pre 
		n > 0,
		liquide + n <= maxLiquide
	post
		liquide! = liquide + n,
		solde! = solde + n;
		
  schema !miseAZeroLiquide
	post liquide! = 0;

  schema !modifierNIP(nNIP : int)
    pre nNIP ~= nip
    post nip! = nNIP;
function  solde, nip, ouverture, fermeture, liquide


end; 	
     


final class Banque ^=

abstract

  var comptes : map of (NumCompte -> Compte),
      soldeG : int,
	  soldeV : int,
	  entrees : int,
	  sorties : int,
	  gains : int,
	  frais : int,
	  dateExercice : Date;
	  

  invariant 
  
  // En fait cet invariant n'est pas nécessaire. C'est le type NumCompte 
  // qui précise cette contrainte. Pas besoin de la répéter. 
  
      #comptes <= maxNum,
	  soldeG - entrees + sorties = soldeV;
	  
	  
       
	   
interface

  function comptes ;
  function frais;

 build{}
    post 
		comptes! = map  of (NumCompte -> Compte){},
		soldeG! = 0,
		soldeV! = 0,
		entrees! = 0,
		sorties! = 0,
		gains! = 0,
		frais!= 1,
		dateExercice! = Date{1, 3, 1900};
	

  schema !ouvrirCompte(soldeInit : int , nc : NumCompte, o:Date)
    pre soldeInit >= minSolde , #comptes < maxNum, 
		nc ~in comptes.dom
    post 
           comptes!= comptes.append(nc -> Compte{soldeInit,0, o, null}) ;
		   
schema !fermerCompte( nc : NumCompte,f:Dat)
    pre 
		nc in comptes.dom, 
		comptes[nc].solde = minSolde, 
		comptes[nc].fermeture = null, 
		f within Date
    post 
           comptes!= 
comptes.remove(nc).append(nc -> Compte{0,comptes[nc].nip,
comptes[nc].ouverture, f}) ;
		   
 schema !supprimerCompte(nc : NumCompte, d:Date)
    pre nc in comptes.dom, comptes[nc].solde = minSolde,
	    comptes[nc].fermeture within Date,
	    ( (let f ^= (comptes[nc].fermeture is Date);
		   (d.a > f.a +2) | 
		    (d.a = f.a+2 & 
			  d.m > f.m) | 
			(d.a = f.a+2 & 
			  d.m = f.m &
			  d.j > f.j)
			)
		)
    post 
           comptes!= comptes.remove(nc) ;
	
  schema !retraitC(nc : NumCompte, n : int)
	pre 
		nc in comptes.dom,
		n > 0,
		comptes[nc].solde - n >= minSolde
	post 
		soldeG! = soldeG - n,
		sorties! = sorties + n,
		comptes[nc]!retrait(n);

  schema !depotC(nc : NumCompte, n : int)
	pre 
		nc in comptes.dom,
		n > 0
	post 
		soldeG! = soldeG + n,
		entrees! = entrees + n,
		comptes[nc]!depot(n);
	
  schema !depotLC(nc : NumCompte, n : int)
	pre 
		nc in comptes.dom,
		n > 0,
		comptes[nc].liquide + n <= maxLiquide
	post
		soldeG! = soldeG + n,
		entrees! = entrees + n,
		comptes[nc]!depotLiquide(n);
	
  schema !virementC(nc1 : NumCompte, nc2 : NumCompte, n : int)
	pre 
		nc1 in comptes.dom,
		nc2 in comptes.dom,
		n > 0,
		comptes[nc1].solde - n >= minSolde
	post
		comptes[nc1]!retrait(n) then comptes[nc2]!depot(n);
		


  schema !ch_NIP(nc :NumCompte , nNIP : int)
    pre 
		nc in comptes.dom, 
		nNIP ~= comptes[nc].nip
    post 
		comptes[nc]!modifierNIP(nNIP);
	  
  schema !bilanV(d: Date)
    pre 
		d.j = dateExercice.j,
		d.m = dateExercice.m,
		soldeG = (+ over (for i::comptes.dom yield comptes[i].solde))
	post 
		soldeG! = (+ over (for i::comptes.dom yield comptes[i].solde)),
		soldeV! = soldeG,
		entrees! = 0,
		sorties! = 0;

  schema !versExterieur(nc : NumCompte, n : int)
	pre 
		nc in comptes.dom,
		comptes[nc].solde - (n + frais) >= minSolde, 
		comptes[nc].fermeture = null,
		(n + frais) > 0
	post 
		soldeG! = soldeG - n +  frais ,
		entrees! = entrees + frais,
		sorties! = sorties + n,
		comptes[nc]!retrait(n+frais);
  
  schema !versInterieur(nc : NumCompte, n : int)
	pre 
		nc in comptes.dom,
		comptes[nc].solde + n >=  frais, 
		comptes[nc].fermeture = null,
		(n - frais) > 0
	post 
		soldeG! = soldeG + n ,
		entrees! = entrees + n ,
		sorties! = sorties,
		comptes[nc]!depot(n-frais);

end;

final class ATM ^=

abstract

 var b : Banque ,
      ett : ETAT ,
      nCompte : NCompte;
invariant 
 
  ( nCompte = null | 1 <= (nCompte is NumCompte) <= maxNum);

  function encNIP(nip : nat) : nat ^= nip*nip ;



interface

  schema !retirer(montant:int)
    pre 
	   montant > 0,
	   ett = retrait@ETAT,
	   nCompte ~= null,
	   (nCompte is NumCompte) in b.comptes.dom,
	   b.comptes[nCompte is NumCompte].fermeture = null,
	   (minSolde + montant) < b.comptes[nCompte is int].solde
	post
	  ett! = inactif@ETAT,
	  b!retraitC(nCompte is NumCompte, montant);
	  
  schema !changer_nip(nip:int)
     pre
	   ett = chNIP@ETAT,
	   nCompte ~= null,
	   nip >=0,
	   (nCompte is NumCompte) in b.comptes.dom,
	   b.comptes[nCompte is NumCompte].fermeture = null,
	   encNIP(nip) ~= b.comptes[(nCompte is NumCompte)].nip,
	   b.comptes[nCompte is NumCompte].nip ~= nip

	 post
	   
	   b!ch_NIP((nCompte is NumCompte), nip),
	   ett! = inactif@ETAT;
		
  schema !insererCarte(nCarte : NumCompte , msg! : string)
    pre ett = ETAT inactif, nCarte in b.comptes.dom
    post ett! = carteIns@ETAT , nCompte! = nCarte , msg! = "Entrer NIP" ;

  schema !NIP_OK(pin : nat , msg! : string)
    pre 

	ett = carteIns@ETAT , 
	nCompte within NumCompte,
	 (nCompte is NumCompte)  in b.comptes.dom,
	 b.comptes[nCompte is NumCompte].fermeture = null,
	encNIP(pin) = b.comptes[(nCompte is NumCompte)].nip
    post ett! = nipOk@ETAT , msg! = "Choisissez un service" ;

  schema !NIP_KO(pin : nat , msg! : string)
    pre 

	ett = ETAT carteIns , 
	nCompte within NumCompte,
	(nCompte is NumCompte) in b.comptes.dom,
	b.comptes[nCompte is NumCompte].fermeture = null,
	encNIP(pin) ~= b.comptes[nCompte is NumCompte].nip
    post ett! = inactif@ETAT , msg! = "NIP incorrect, carte rejetée." ;

  schema !choixService( bout : BOUTON )
    pre 

	ett =  nipOk@ETAT
    post ett! = ( [bout =  cash@BOUTON] : retrait@ETAT ,
                    [bout =  nip@BOUTON] :  chNIP@ETAT
                  ) ;


	
  build{!b:Banque}
	
    post 

         ett! = inactif@ETAT , nCompte! = null ;

end;

final class Transaction ^=

abstract

  var banques : map of (NumBanque -> Banque)
  
  interface

schema !versExterieur(b1 : NumBanque, b2 : NumBanque, numCompte1 : NumCompte, numCompte2 : NumCompte, montant: int)
	pre 
		~(b1 = b2),
		b1 in banques.dom,
		b2 in banques.dom,
		numCompte1 in banques[b1].comptes,
		numCompte2 in banques[b2].comptes,
		banques[b1].comptes[numCompte1].fermeture = null,
		banques[b2].comptes[numCompte2].fermeture = null,
		banques[b1].comptes[numCompte1].solde - (montant + banques[b1].frais) >= minSolde,
		0 < (banques[b1].frais + montant),
		banques[b2].frais <= (banques[b2].comptes[numCompte2].solde + montant),
		banques[b2].frais < montant
	post 
		banques[b1]!versExterieur(numCompte1, montant),
		banques[b2]!versInterieur(numCompte2, montant);
 build{}
    post 
		banques! = map  of (NumBanque -> Banque){}
end;

// End